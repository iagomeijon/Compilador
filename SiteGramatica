PROGRAMA ->  BLOCO_VARIAVEIS BLOCO_PROTOTIPO_DEC_FUNC var BLOCO_VARIAVEIS inicio BLOCO_COMANDO fim .
BLOCO_DEC_FUNC -> DEC_FUNCAO BLOCO_DEC_FUNC .
BLOCO_DEC_FUNC -> .
DEC_FUNCAO -> PROT_FUNC var BLOCO_VARIAVEIS inicio BLOCO_COMANDO fim .
BLOCO_PROTOTIPO_DEC_FUNC -> PROT_FUNC ponto BLOCO_PROTOTIPO_DEC_FUNC .
BLOCO_PROTOTIPO_DEC_FUNC -> BLOCO_DEC_FUNC .
PROT_FUNC -> funcao id ( LISTAR_PARAMETROS ) : TIPO_RETORNO .
TIPO_RETORNO -> TIPO .
TIPO_RETORNO -> nulo .
LISTAR_PARAMETROS ->  PARAMETROS .
LISTAR_PARAMETROS -> .
PARAMETROS -> VETOR TIPO : id ; PARAMETROS .
PARAMETROS -> VETOR TIPO : id .
VETOR -> vet .
VETOR -> .
RECEBA -> receba .
RECEBA -> receba ( EXP ) .
ESCREVA -> escreva LISTA_ESCREVA .
LISTA_ESCREVA -> string ; LISTA_ESCREVA.
LISTA_ESCREVA -> EXP ; LISTA_ESCREVA.
LISTA_ESCREVA -> string .
LISTA_ESCREVA -> EXP .
LEIA ->  leia CONCATENAR_LEIA .
CONCATENAR_LEIA -> id ; CONCATENAR_LEIA .
CONCATENAR_LEIA -> id [ EXP ] ; CONCATENAR_LEIA .
CONCATENAR_LEIA -> id .
CONCATENAR_LEIA -> id [ EXP ] .
BLOCO_ATEH -> para id de EXP ateh EXP inicio BLOCO_COMANDO fim .
BLOCO_ENQUANTO -> enquanto EXP_RELACIONAL_TOTAL inicio BLOCO_COMANDO fim .
BLOCO_IF -> se EXP_RELACIONAL_TOTAL inicio BLOCO_COMANDO fim BLOCO_SENAO .
BLOCO_SENAO -> senao inicio BLOCO_COMANDO fim .
BLOCO_SENAO -> .
BLOCO_COMANDO -> CHAMADA_FUNC ponto BLOCO_COMANDO .
BLOCO_COMANDO -> ATRIBUICAO ponto BLOCO_COMANDO .
BLOCO_COMANDO -> BLOCO_IF  BLOCO_COMANDO .
BLOCO_COMANDO -> BLOCO_ENQUANTO BLOCO_COMANDO.
BLOCO_COMANDO -> BLOCO_ATEH BLOCO_COMANDO .
BLOCO_COMANDO -> LEIA ponto  BLOCO_COMANDO .
BLOCO_COMANDO -> ESCREVA ponto BLOCO_COMANDO .
BLOCO_COMANDO -> pare ponto BLOCO_COMANDO .
BLOCO_COMANDO -> RECEBA ponto BLOCO_COMANDO.
BLOCO_COMANDO -> .
EXP_RELACIONAL_TOTAL -> EXP_RELACIONAL_N2 or EXP_RELACIONAL_TOTAL.
EXP_RELACIONAL_TOTAL -> EXP_RELACIONAL_N2.
EXP_RELACIONAL_N2 ->  EXP_RELACIONAL_N3 & EXP_RELACIONAL_N2.
EXP_RELACIONAL_N2 -> EXP_RELACIONAL_N3.
EXP_RELACIONAL_N3 -> NEGADO EXP_RELACIONAL_FINAL.
EXP_RELACIONAL_N3 -> EXP_RELACIONAL_FINAL.
EXP_RELACIONAL_FINAL -> EXP_RELACIONAL_SIMPLES.
EXP_RELACIONAL_FINAL -> ( EXP_RELACIONAL_TOTAL ) .
EXP_RELACIONAL_SIMPLES -> EXP RELACIONAIS EXP .
NEGADO -> ! .
NEGADO -> ! NEGADO.
RELACIONAIS -> = .
RELACIONAIS -> < .
RELACIONAIS -> <= .
RELACIONAIS -> > .
RELACIONAIS -> >= .
RELACIONAIS -> <> .
ATRIBUICAO -> id INDEX <- EXP .
INDEX -> [ EXP ] .
INDEX -> .
EXP -> EXP_N2 SOMA_SUB EXP .
EXP -> EXP_N2 .
EXP_N2 -> EXP_N3 MULTI_DIV_REST EXP_N2 .
EXP_N2 -> EXP_N3.
EXP_N3 -> EXP_FINAL ** EXP_N3 .
EXP_N3 -> EXP_FINAL .
SOMA_SUB -> + .
SOMA_SUB -> - .
MULTI_DIV_REST -> *.
MULTI_DIV_REST -> /.
MULTI_DIV_REST -> %.
EXP_FINAL -> SINAL id .
EXP_FINAL -> SINAL NUM .
EXP_FINAL -> SINAL id [ EXP ] .
EXP_FINAL -> SINAL CHAMADA_FUNC .
EXP_FINAL ->  ( EXP ) .
EXP_FINAL -> - ( EXP ) .
SINAL -> - .
SINAL -> .
CHAMADA_FUNC ->  id ( LISTAR_PARAMETROS_CHAMADA ) .
LISTAR_PARAMETROS_CHAMADA -> CONCATENAR_EXP .
LISTAR_PARAMETROS_CHAMADA -> .
CONCATENAR_EXP -> EXP ; CONCATENAR_EXP .
CONCATENAR_EXP -> EXP .
BLOCO_VARIAVEIS -> VARIAVEL ponto BLOCO_VARIAVEIS .
BLOCO_VARIAVEIS -> .
VARIAVEL -> DECLARACAO_ID .
VARIAVEL -> DECLARACAO_VET .
DECLARACAO_VET -> vet TIPO : id numero_inteiro .
DECLARACAO_ID -> TIPO : CONCATENAR_ID .
CONCATENAR_ID -> id ; CONCATENAR_ID .
CONCATENAR_ID -> id .
NUM -> numero_inteiro .
NUM -> numero_real .
TIPO -> inteiro .
TIPO -> real .